# Author: mosra <mosra@centrum.cz>
pkgname=android-arm64-magnum-extras
pkgver=dev
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine (Android ARM64)"
arch=('any')
url="https://magnum.graphics"
license=('MIT')
depends=('android-arm64-magnum')
makedepends=('cmake' 'ninja' 'android-ndk' 'corrade')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-android-arm64" ] ; then
        mkdir "$_rootdir/build-android-arm64"
        cd "$_rootdir/build-android-arm64"

        cmake .. \
            -DCMAKE_SYSTEM_NAME=Android \
            -DCMAKE_SYSTEM_VERSION=22 \
            -DCMAKE_ANDROID_ARCH_ABI=arm64-v8a \
            -DCMAKE_ANDROID_NDK_TOOLCHAIN_VERSION=clang \
            -DCMAKE_ANDROID_STL_TYPE=c++_static \
            -G Ninja
    fi

    cd "$_rootdir/build-android-arm64"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/platforms/android-22/arch-arm64/usr \
        -DMAGNUM_INCLUDE_INSTALL_PREFIX=/opt/android-ndk/sysroot/usr \
        -DWITH_PLAYER=OFF \
        -DWITH_UI=ON \
        -DWITH_UI_GALLERY=OFF \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON
    ninja
}

package() {
    cd "$_rootdir/build-android-arm64"
    DESTDIR="$pkgdir/" ninja install/strip
}

# kate: hl bash
